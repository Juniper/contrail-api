#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#

import os
import glob

from SCons.Node.FS import Entry as entry_class
from SCons.Node.FS import File as file_class
from SCons.Node.FS import Dir as dir_class

Import('BuildEnv')
env = BuildEnv.Clone()

cd_cmd = 'cd ' + Dir('.').path + ' && '

env_top = Dir(env['TOP']).abspath
repo_top = env['api_repo_path']
api_lib_path =  Dir(repo_top).abspath + '/api-lib'

autogen_infra_sources = [
    repo_top + '/generateds/generateDS.py',
    repo_top + '/generateds/TypeGenerator.py',
    repo_top + '/generateds/ccmap.py',
    repo_top + '/generateds/idl_parser.py',
    repo_top + '/generateds/ifmap_global.py',
    repo_top + '/generateds/ifmap_model.py',
    repo_top + '/generateds/ifmap_frontend.py',
]

autogen_sources = [
    repo_top + '/schema/all_cfg.xsd',
    repo_top + '/schema/vnc_cfg.xsd',
    repo_top + '/schema/bgp_schema.xsd',
    repo_top + '/schema/ietf-l3vpn-schema.xsd',
    repo_top + '/schema/smi-base.xsd',
]

setup_sources = [
    'doc',
    'etc',
    'vnc_api',
    '.coveragerc',
    '.pylintrc',
    '.stestr.conf',
    'MANIFEST.in',
    'README.md',
    'requirements.txt',
    'setup.cfg',
    'setup.py',
    'test-requirements.txt',
    'tox.ini',
]

# Make sure XML and YAML schema are in sync
yaml_schema_dir = Dir(env['TOP']).abspath + '/schema/yaml'
generated_yaml_schema_files = [yaml_schema_dir + '/base.yml']
# Remove target files, to force scons to build SyncSchema always
#for tgt_file in generated_yaml_schema_files:
#    if os.path.exists(tgt_file):
#        os.remove(tgt_file)
sync_yaml_schema = env.SyncSchema(generated_yaml_schema_files,
                             repo_top + '/schema/all_cfg.xsd')

# Download dependent go modules
go_mod = env.GoMod(
    api_lib_path + '/go.sum',
    env_top + '/api-lib/gomod.logs',
    sub_cmd='download',
    retries=5)

# Install contrailschema binary, which is used to gen code from schema
# with template
contrailschema_src = 'github.com/Juniper/asf/cmd/contrailschema'
contrailschema_install_cmd = 'go install %s' % contrailschema_src

go_codegen_tool = env.Command(
    'contrailschema',
    None,
    contrailschema_install_cmd,
    chdir=api_lib_path)
env.Depends(go_codegen_tool, [sync_yaml_schema, go_mod])

# Generate proto, client and service interfaces
models_import_path = 'github.com/Juniper/contrail-api-client/api-lib/pkg/models'
services_import_path = '/github.com/Juniper/contrail-api-client/api-lib/pkg/services'
go_vnc_client_cmd = "ENV_TOP=%s contrailschema generate --no-regenerate \
     --schemas %s \
    --models-import-path %s \
    --services-import-path %s \
    --template-config" % (env_top + '/api-lib',
                          yaml_schema_dir,
                          models_import_path,
                          services_import_path)

template_config = api_lib_path + '/tools/go_vncapi_gen_template.yaml'

go_vnc_api_files =  ['/api-lib/pkg/vncapi/gen_client_http.go',
                     '/api-lib/pkg/services/gen_service_interface.go']
proto_files = ['/api-lib/proto/'+ models_import_path + '/gen_model.proto',
               '/api-lib/proto/' + services_import_path + '/gen_service.proto']
gen_go_vnc_api_files = [env_top + go_file for go_file in go_vnc_api_files]
gen_proto_files = [env_top + proto_file for proto_file in proto_files]

go_vnc_client_gen = env.Command(
    [gen_go_vnc_api_files, gen_proto_files],
    template_config,
    go_vnc_client_cmd + ' $SOURCE')
env.Depends(go_vnc_client_gen, go_codegen_tool)

# Generate models and services struct from proto
protoc_go_gen = env.ProtocGenGoVncApi(gen_proto_files)
env.Depends(protoc_go_gen, go_vnc_client_gen)

#Make sure generated go vncapi client code is up-to-date with YAML schema
pb_go_files = [api_lib_path + '/pkg/models/gen_model.pb.go',
               api_lib_path + '/pkg/services/gen_service.pb.go']
vnc_api_files_go = [repo_top + go_file for go_file in go_vnc_api_files] 
xsd_schemas = [repo_top + xsd for xsd in glob.glob("schema/*.xsd")]
sync_goclient_schema = env.SyncGoVncClient([vnc_api_files_go, pb_go_files], xsd_schemas)
env.Depends(sync_goclient_schema, protoc_go_gen)

setup_sources_rules = []
for file in setup_sources:
    entry = Entry(repo_top + '/api-lib/%s' % file)
    path = Entry(repo_top + '/api-lib/%s' % file).abspath
    if isinstance(entry, (entry_class, file_class)):
        setup_sources_rules.append(env.Install(Dir('.'), entry))
    elif isinstance(entry, dir_class):
        setup_sources_rules.append(env.Install('vnc_api', Glob('%s/*' % path)))

autogen_script = File(repo_top + '/generateds/generateDS.py').path
autogen_cmd = '%s -f -o %s -g ifmap-frontend' % (
    autogen_script, Dir('vnc_api/gen/resource').path)

env.Append(ENV={'HEAT_BUILDTOP': Dir(env['TOP']).abspath})

generated_files = ['vnc_api/gen/__init__.py']
generated_rule = env.Command(generated_files,
                             repo_top + '/schema/all_cfg.xsd',
                             autogen_cmd + ' $SOURCE')


remote_setup_sources = [
    ("", repo_top + '/LICENSE'),
    ("", repo_top + '/base/version.info'),
    ("vnc_api/gen", repo_top + '/generateds/generatedssuper.py'),
    ("vnc_api/gen", repo_top + '/generateds/cfixture.py'),
]
remote_setup_sources_rules = []
for dest, file in remote_setup_sources:
    remote_setup_sources_rules.append(env.Install(dest, file))

doc_sources_rules = SConscript(dirs=['doc'], exports='BuildEnv')

sdist_depends = [remote_setup_sources_rules]
sdist_depends.extend(generated_rule)
sdist_depends.extend(setup_sources_rules)
sdist_depends.extend(doc_sources_rules)

version_file_path = File(repo_top + '/base/version.info').abspath
with open(version_file_path, 'r') as f:
    version = f.read().strip('\n').strip('\t')
sdist_gen = env.Command('dist/contrail-api-client-{}.tar.gz'.format(version),
                        'setup.py', cd_cmd + 'python setup.py sdist')

env.Default(sdist_gen)

# generated files depend on autogeneration infra parts
env.Depends(autogen_infra_sources, remote_setup_sources_rules)
env.Depends(generated_rule, [autogen_infra_sources, autogen_sources])
env.Depends(setup_sources_rules, autogen_infra_sources)

# install everything before building distribution
env.Depends(sdist_gen, [sync_goclient_schema, sdist_depends])

def SymLink(target, source, env):
    os.symlink(os.path.abspath(str(source[0])), os.path.abspath(str(target[0])))

if 'install' in BUILD_TARGETS:
   install_cmd = env.Command(None, 'setup.py',
                             cd_cmd + 'python setup.py install %s' %
                             env['PYTHON_INSTALL_OPT'])
   env.Depends(install_cmd, sdist_depends)
   env.Alias('install', install_cmd)
   # Install in python3 path
   setup_link_cmd = env.Command("setup3.py", "setup.py", SymLink)
   env.Depends(setup_link_cmd, sdist_depends)
   py3_install_cmd = env.Command(None, 'setup3.py',
                             cd_cmd + 'python3 setup3.py install %s' %
                             env['PYTHON_INSTALL_OPT'])
   env.Depends(py3_install_cmd, sdist_depends)
   env.Alias('install', py3_install_cmd)

env.Alias('install',
    env.Install(env['INSTALL_CONF'], 'etc/contrail/vnc_api_lib.ini'))

env.SetupPyTestSuiteWithDeps(sdist_gen, use_tox=True, sdist_depends=[])
